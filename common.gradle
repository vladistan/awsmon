apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'net.saliman.cobertura'

/**
 * Common Configuration
 */

checkstyle {
  toolVersion = '6.14.1'
  project.ext.checkstyleVersion = '6.14'
  ignoreFailures = true
  configFile = file("${project.rootDir}/checkstyle.xml")
  configurations {
    checkstyle
  }
  dependencies {
    assert project.hasProperty("checkstyleVersion")
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
  }
}

cobertura {
  coverageFormats = ['xml', 'html']
}

sonarqube {
  properties {
    property "sonar.java.coveragePlugin", "cobertura"
    property "sonar.cobertura.reportPath", "build/reports/cobertura/coverage.xml"
    property "sonar.scm.provider", "git"
    property "sonar.projectName", artifactId
    property "sonar.projectDescription", artifactId
    property "sonar.projectVersion",  version
    property "sonar.projectKey", group + ':' + artifactId
  }
}

compileJava {
  options.fork = true
  options.incremental = true
}

test {
  testLogging.showStandardStreams = true
  minHeapSize = "128m"
  maxHeapSize = "1024m"
  jvmArgs '-XX:MaxPermSize=256m'

}


sourceCompatibility = 1.7
targetCompatibility = 1.7

/**
 * Common Tasks
 */
task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier 'javadoc'
  from javadoc.destinationDir
}

/**
 * Common Artifacts
 */
artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    jar(MavenPublication) {
      groupId group
      artifactId artifactId
      from components.java
    }
  }

}

jar {
  baseName artifactId
}

repositories {
  mavenCentral()
  jcenter()
}
